open HolKernel Parse boolLib bossLib helper_tactics;
open l2Theory invariant_l1Theory invariant_l2Theory proof_obligations_cpu_l2Theory;

val _ = new_theory "invariant_l2_preservation";

(*************************************DEVICE************************************)

Theorem DEVICE_INTERNAL_OPERATION_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics device1 device2 memory environment.
  PROOF_OBLIGATION_INTERNAL_DEVICE_OPERATION_L2 device_characteristics /\
  device_transition_l2 device_characteristics device1 (memory, device_internal_operation environment) device2 /\
  INVARIANT_L2 device_characteristics memory device1
  ==>
  INVARIANT_L2 device_characteristics memory device2
Proof
REWRITE_TAC [device_transitions_l2_cases] THEN
REWRITE_TAC [pairTheory.PAIR_EQ] THEN
REWRITE_TAC [stateTheory.device_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
ETAC stateTheory.device_transition_labels_type_11 THEN
NRLTAC 2 THEN
IRTAC internal_operation_invariant_l2_preservation_lemmasTheory.INTERNAL_DEVICE_OPERATION_PRESERVES_INVARIANT_L2_LEMMA THEN
STAC
QED

Theorem DEVICE_MEMORY_READ_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics device1 device2 address_bytes memory.
  device_transition_l2 device_characteristics device1 (memory, device_memory_read address_bytes) device2 /\
  INVARIANT_L2 device_characteristics memory device1
  ==>
  INVARIANT_L2 device_characteristics memory device2
Proof
REWRITE_TAC [device_transitions_l2_cases] THEN
REWRITE_TAC [pairTheory.PAIR_EQ] THEN
REWRITE_TAC [stateTheory.device_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM stateTheory.device_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
ETAC stateTheory.device_transition_labels_type_11 THEN
NRLTAC 2 THEN
IRTAC dma_request_served_invariant_l2_preservation_lemmasTheory.DMA_REQUEST_SERVED_L2_PRESERVES_INVARIANT_L2_LEMMA THEN
STAC
QED

Theorem DEVICE_MEMORY_WRITE_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics device1 device2 address_bytes memory1 memory2.
  PROOF_OBLIGATION_READABLE_WRITABLE device_characteristics /\
  device_transition_l2 device_characteristics device1 (memory1, device_memory_write address_bytes) device2 /\
  memory2 = update_memory memory1 address_bytes /\
  INVARIANT_L1 device_characteristics memory1 device1 /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
INTRO_TAC THEN
ITAC dma_write_invariant2_preservation_lemmasTheory.DEVICE_MEMORY_WRITE_DEVICE_PRESERVES_INVARIANT_L2_LEMMA THEN
ITAC dma_write_invariant2_preservation_lemmasTheory.DEVICE_MEMORY_WRITE_MEMORY_PRESERVES_INVARIANT_L2_LEMMA THEN
STAC
QED

Theorem DEVICE_EXTERNAL_BDS_APPENDED_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics device1 device2 memory1 memory2.
  DMAC_CAN_ACCESS_MEMORY_IMPLIES_R_W_EQ device_characteristics memory1 memory2 device1 /\
  NOT_DMAC_CAN_ACCESS_MEMORY_IMPLIES_PRESERVED_R_W_OR_SCRATCHPAD_R_W device_characteristics device1 memory1 memory2 /\
  MEMORY_WRITE_APPENDS_EXTERNAL_CONCRETE_BDS_R_W device_characteristics device1.dma_state.internal_state memory1 memory2 /\
  device_transition_l2 device_characteristics device1 (memory2, external_bds_appended) device2 /\
  ABSTRACT_CONCRETE_BDS_TO_FETCH_EQ device_characteristics memory1 device1 device1 /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [device_transitions_l2_cases] THEN
REWRITE_TAC [pairTheory.PAIR_EQ] THEN
REWRITE_TAC [stateTheory.device_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM stateTheory.device_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
RLTAC THEN
ETAC INVARIANT_L2 THEN
ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_EXTERNAL_ABSTRACT_BDS_L2_PRESERVES_DEFINED_CHANNELS_LEMMA THEN
ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_EXTERNAL_ABSTRACT_BDS_L2_PRESERVES_INVARIANT_FETCH_BD_L2_LEMMA THEN
ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_EXTERNAL_ABSTRACT_BDS_L2_PRESERVES_INVARIANT_UPDATE_BD_L2_LEMMA THEN
ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_EXTERNAL_ABSTRACT_BDS_L2_PRESERVES_INVARIANT_TRANSFER_DATA_L2_LEMMA THEN
ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_EXTERNAL_ABSTRACT_BDS_L2_PRESERVES_INVARIANT_WRITE_BACK_BD_L2_LEMMA THEN
ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_EXTERNAL_ABSTRACT_BDS_L2_PRESERVES_INVARIANT_SCRATCHPAD_R_L2_LEMMA THEN
ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_EXTERNAL_ABSTRACT_BDS_L2_PRESERVES_INVARIANT_SCRATCHPAD_W_L2_LEMMA THEN
STAC
QED

Theorem DEVICE_REGISTER_READ_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics device1 device2 cpu_dma_address_bytes memory.
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_BD_INTERPRETATION device_characteristics /\ 
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_SCRATCHPAD device_characteristics /\
  device_transition_l2 device_characteristics device1 (memory, device_register_read cpu_dma_address_bytes) device2 /\
  INVARIANT_L2 device_characteristics memory device1
  ==>
  INVARIANT_L2 device_characteristics memory device2
Proof
REWRITE_TAC [device_transitions_l2_cases] THEN
REWRITE_TAC [pairTheory.PAIR_EQ] THEN
REWRITE_TAC [stateTheory.device_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM stateTheory.device_transition_labels_type_distinct] THEN
REWRITE_TAC [stateTheory.device_transition_labels_type_11] THEN
INTRO_TAC THEN
AXTAC THEN
NRLTAC 2 THEN
WEAKEN_TAC is_disj THEN
PTAC l2Theory.device_register_read_l2 THEN
ETAC INVARIANT_L2 THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_PRESERVES_DEFINED_CHANNELS_LEMMA THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_PRESERVES_INVARIANT_FETCH_BD_L2_LEMMA THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_PRESERVES_INVARIANT_UPDATE_BD_L2_LEMMA THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_PRESERVES_INVARIANT_TRANSFER_DATA_L2_LEMMA THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_PRESERVES_INVARIANT_WRITE_BACK_BD_L2_LEMMA THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_PRESERVES_INVARIANT_SCRATCHPAD_R_L2_LEMMA THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_PRESERVES_INVARIANT_SCRATCHPAD_W_L2_LEMMA THEN
STAC
QED




(***********************************SYSTEM**************************************)





Theorem SYSTEM_CPU_INTERNAL_OPERATION_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 cpu_transition.
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) cpu_internal_operation (cpu2, memory2, device2) /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [operationsTheory.system_transitions_cases] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
EQ_PAIR_ASM_TAC THEN
ALL_RLTAC THEN
STAC
QED

Theorem SYSTEM_CPU_MEMORY_READ_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 cpu_transition address_bytes.
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) (cpu_memory_read address_bytes) (cpu2, memory2, device2) /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [operationsTheory.system_transitions_cases] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM operationsTheory.system_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
EQ_PAIR_ASM_TAC THEN
ETAC operationsTheory.system_transition_labels_type_11 THEN
ALL_RLTAC THEN
STAC
QED

Theorem SYSTEM_CPU_MEMORY_WRITE_PRESERVES_INVARIANT_L2_LEMMA:
!cpu_transition INVARIANT_CPU device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 address_bytes.
  PROOF_OBLIGATION_CPU_PRESERVES_R_W_WHEN_DMAC_ACCESSES_MEMORY INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_CPU_PRESERVES_R_W_OR_SUPERSET_OF_SCRATCHPAD INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_CPU_MEMORY_WRITE_APPENDS_EXTERNAL_CONCRETE_BDS_R_W INVARIANT_CPU cpu_transition device_characteristics /\
  INVARIANT_CPU memory1 cpu1 /\
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) (cpu_memory_write address_bytes) (cpu2, memory2, device2) /\
  ABSTRACT_CONCRETE_BDS_TO_FETCH_EQ device_characteristics memory1 device1 device1 /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [operationsTheory.system_transitions_cases] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM operationsTheory.system_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
EQ_PAIR_ASM_TAC THEN
ETAC operationsTheory.system_transition_labels_type_11 THEN
ALL_RLTAC THEN
Cases_on ‘EXTERNAL_BDS device_characteristics’ THENL
[
 ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.PROOF_OBLIGATION_CPU_PRESERVES_R_W_WHEN_DMAC_ACCESSES_MEMORY_IMPLIES_DMAC_CAN_ACCESS_MEMORY_IMPLIES_R_W_EQ_LEMMA THEN
 ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.PROOF_OBLIGATION_CPU_PRESERVES_R_W_OR_SUPERSET_OF_SCRATCHPAD_IMPLIES_NOT_DMAC_CAN_ACCESS_MEMORY_IMPLIES_PRESERVED_R_W_OR_SCRATCHPAD_R_W_LEMMA THEN
 ITAC append_external_bds_invariant_l2_preservation_lemmasTheory.PROOF_OBLIGATION_CPU_MEMORY_WRITE_APPENDS_EXTERNAL_CONCRETE_BDS_R_W_IMPLIES_MEMORY_WRITE_APPENDS_EXTERNAL_CONCRETE_BDS_R_W_LEMMA THEN
 MATCH_MP_TAC DEVICE_EXTERNAL_BDS_APPENDED_PRESERVES_INVARIANT_L2_LEMMA THEN
 INST_EXISTS_TAC THEN
 STAC
 ,
 IRTAC cpu_invariant_l2_preservation_lemmasTheory.CPU_MEMORY_WRITE_PRESERVES_INVARIANT_L2_LEMMA THEN
 IRTAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_NOT_EXTERNAL_BDS_PRESERVES_INVARIANT_L2_LEMMA THEN
 STAC
]
QED

Theorem SYSTEM_DEVICE_INTERNAL_OPERATION_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 cpu_transition.
  PROOF_OBLIGATION_INTERNAL_DEVICE_OPERATION_L2 device_characteristics /\
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) device_internal_operation (cpu2, memory2, device2) /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [operationsTheory.system_transitions_cases] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM operationsTheory.system_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
EQ_PAIR_ASM_TAC THEN
ALL_RLTAC THEN
IRTAC DEVICE_INTERNAL_OPERATION_PRESERVES_INVARIANT_L2_LEMMA THEN
STAC
QED

Theorem SYSTEM_CPU_REGISTER_READ_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 INVARIANT_CPU cpu_transition cpu_dma_address_bytes.
  PROOF_OBLIGATION_READABLE_WRITABLE device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_BDS_TO_FETCH device_characteristics /\
  PROOF_OBLIGATION_CPU_REGISTER_READ_MEMORY_WRITE_APPENDS_CONCRETE_BDS_R_W INVARIANT_CPU cpu_transition device_characteristics /\
  ABSTRACT_CONCRETE_BDS_TO_FETCH_EQ device_characteristics memory1 device1 device1 /\
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) (cpu_register_read_memory_write cpu_dma_address_bytes) (cpu2, memory2, device2) /\
  INVARIANT_CPU memory1 cpu1 /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [operationsTheory.system_transitions_cases] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM operationsTheory.system_transition_labels_type_distinct] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_11] THEN
REWRITE_TAC [pairTheory.PAIR_EQ] THEN
INTRO_TAC THEN
AXTAC THEN
PAT_X_ASSUM “x = (y, z)” (fn thm => ASSUME_TAC (GSYM thm)) THEN
NRLTAC 7 THEN
ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_DEVICE_REGISTER_READ_IMPLIES_PRESERVATION_OR_REGISTER_READ_LEMMA THEN
SPLIT_ASM_DISJS_TAC THENL
[
 IRTAC write_properties_lemmasTheory.EMPTY_DMA_ADDRESS_BYTES_PRESERVES_MEMORY_LEMMA THEN
 RLTAC THEN
 IRTAC device_register_read_invariant_l2_preservation_lemmasTheory.ABSTRACT_CONCRETE_BDS_TO_FETCH_INTERNAL_STATE_CHANNELS_EQ_EXTERNAL_BDS_APPENDED_PRESERVES_INVARIANT_L2_LEMMA THEN
 STAC
 ,
 AXTAC THEN
 Cases_on ‘EXTERNAL_BDS device_characteristics’ THENL
 [
  ITAC device_register_read_invariant_l2_preservation_lemmasTheory.REGISTER_READ_IMPLIES_ALL_BDS_TO_FETCH_EQ_LEMMA THEN
  PTAC PROOF_OBLIGATION_CPU_REGISTER_READ_MEMORY_WRITE_APPENDS_CONCRETE_BDS_R_W THEN
  AITAC THEN
  ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_DEVICE_REGISTER_READ_IMPLIES_DEVICE_REGISTER_READ_L2_LEMMA THEN
  ITAC device_register_read_invariant_l2_preservation_lemmasTheory.INVARIANT_L2_IMPLIES_INVARIANT_SCRATCHPAD_W_L2_LEMMA THEN
  ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_L2_IMPLIES_R_W_EQ_LEMMA THEN
  ITAC DEVICE_REGISTER_READ_PRESERVES_INVARIANT_L2_LEMMA THEN
  IRTAC device_register_read_invariant_l2_preservation_lemmasTheory.ABSTRACT_CONCRETE_BDS_TO_FETCH_R_W_APPENDS_EXTERNAL_CONCRETE_BDS_CHANNELS_EQ_PRESERVES_INVARIANT_L2_LEMMA THEN
  STAC
  ,
  ITAC device_register_read_invariant_l2_preservation_lemmasTheory.INVARIANT_L2_IMPLIES_INVARIANT_SCRATCHPAD_W_L2_LEMMA THEN
  ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_DEVICE_REGISTER_READ_IMPLIES_DEVICE_REGISTER_READ_L2_LEMMA THEN
  ITAC device_register_read_invariant_l2_preservation_lemmasTheory.DEVICE_REGISTER_READ_L2_IMPLIES_R_W_EQ_LEMMA THEN
  MATCH_MP_TAC append_external_bds_invariant_l2_preservation_lemmasTheory.APPEND_NOT_EXTERNAL_BDS_PRESERVES_INVARIANT_L2_LEMMA THEN
  PAXTAC THEN
  CONJS_TAC THEN TRY STAC THEN
  IRTAC DEVICE_REGISTER_READ_PRESERVES_INVARIANT_L2_LEMMA THEN
  IRTAC device_register_read_invariant_l2_preservation_lemmasTheory.R_W_EQ_PRESERVES_INVARIANT_L2_LEMMA THEN
  STAC
 ]
]
QED

Theorem SYSTEM_CPU_REGISTER_WRITE_PRESERVES_INVARIANT_L2_LEMMA:
!INVARIANT_CPU device_characteristics cpu_transition cpu1 cpu2 cpu_dma_address_bytes memory1 memory2 device1 device2.
  PROOF_OBLIGATION_CPU_REGISTER_WRITE_SCRATCHPAD_R_W INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_CPU_REGISTER_WRITE_MEMORY_WRITE_APPENDS_CONCRETE_BDS_R_W INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_INTERNAL_BDS_INDEPENDENT_OF_MEMORY device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_READABLE_WRITABLE device_characteristics /\
  ABSTRACT_CONCRETE_BDS_TO_FETCH_EQ device_characteristics memory1 device1 device1 /\
  INVARIANT_CPU memory1 cpu1 /\
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) (cpu_register_write_memory_write cpu_dma_address_bytes) (cpu2, memory2, device2) /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
INTRO_TAC THEN
ETAC INVARIANT_L2 THEN
ITAC device_register_write_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_CPU_REGISTER_WRITE_MEMORY_WRITE_PRESERVES_DEFINED_CHANNELS_LEMMA THEN
ITAC device_register_write_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_CPU_REGISTER_WRITE_MEMORY_WRITE_PRESERVES_INVARIANT_FETCH_BD_L2_LEMMA THEN
ITAC device_register_write_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_CPU_REGISTER_WRITE_MEMORY_WRITE_PRESERVES_INVARIANT_UPDATE_BD_L2_LEMMA THEN
ITAC device_register_write_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_CPU_REGISTER_WRITE_MEMORY_WRITE_PRESERVES_INVARIANT_TRANSFER_DATA_L2_LEMMA THEN
ITAC device_register_write_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_CPU_REGISTER_WRITE_MEMORY_WRITE_PRESERVES_INVARIANT_WRITE_BACK_BD_L2_LEMMA THEN
IRTAC device_register_write_invariant_l2_preservation_lemmasTheory.DEVICE_TRANSITION_L2_CPU_REGISTER_WRITE_MEMORY_WRITE_PRESERVES_INVARIANT_SCRATCHPAD_R_W_L2_LEMMA THEN
STAC
QED

Theorem SYSTEM_DEVICE_MEMORY_READ_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 cpu_transition address_bytes.
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) (device_memory_read address_bytes) (cpu2, memory2, device2) /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [operationsTheory.system_transitions_cases] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM operationsTheory.system_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
EQ_PAIR_ASM_TAC THEN
ETAC operationsTheory.system_transition_labels_type_11 THEN
ALL_RLTAC THEN
IRTAC DEVICE_MEMORY_READ_PRESERVES_INVARIANT_L2_LEMMA THEN
STAC
QED

Theorem SYSTEM_DEVICE_MEMORY_WRITE_PRESERVES_INVARIANT_L2_LEMMA:
!device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 cpu_transition address_bytes.
  PROOF_OBLIGATION_READABLE_WRITABLE device_characteristics /\
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) (device_memory_write address_bytes) (cpu2, memory2, device2) /\
  INVARIANT_L1 device_characteristics memory1 device1 /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
REWRITE_TAC [operationsTheory.system_transitions_cases] THEN
REWRITE_TAC [operationsTheory.system_transition_labels_type_distinct] THEN
REWRITE_TAC [GSYM operationsTheory.system_transition_labels_type_distinct] THEN
INTRO_TAC THEN
AXTAC THEN
EQ_PAIR_ASM_TAC THEN
ETAC operationsTheory.system_transition_labels_type_11 THEN
ALL_RLTAC THEN
IRTAC DEVICE_MEMORY_WRITE_PRESERVES_INVARIANT_L2_LEMMA THEN
STAC
QED

Theorem PROOF_OBLIGATIONS_DMA_L2_PRESERVATION_INVARIANT_L2_LEMMA:
!device_characteristics.
  PROOF_OBLIGATIONS_DMA_L2 device_characteristics
  ==>
  PROOF_OBLIGATION_READABLE_WRITABLE device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_BDS_TO_FETCH device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_INTERNAL_BDS_INDEPENDENT_OF_MEMORY device_characteristics
Proof
INTRO_TAC THEN
PTAC proof_obligations_dma_l2Theory.PROOF_OBLIGATIONS_DMA_L2 THEN
STAC
QED

Theorem SYSTEM_PRESERVES_INVARIANT_L2_LEMMA:
!cpu_transition INVARIANT_CPU device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 operation.
  PROOF_OBLIGATIONS_CPU_L2 INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATIONS_DMA_L2 device_characteristics /\
  PROOF_OBLIGATION_INTERNAL_DEVICE_OPERATION_L2 device_characteristics /\
  INVARIANT_CPU memory1 cpu1 /\
  ABSTRACT_CONCRETE_BDS_TO_FETCH_EQ device_characteristics memory1 device1 device1 /\
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) operation (cpu2, memory2, device2) /\
  INVARIANT_L1 device_characteristics memory1 device1 /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L2 device_characteristics memory2 device2
Proof
INTRO_TAC THEN
ITAC PROOF_OBLIGATIONS_DMA_L2_PRESERVATION_INVARIANT_L2_LEMMA THEN
ETAC PROOF_OBLIGATIONS_CPU_L2 THEN
Cases_on ‘operation’ THENL
[
 IRTAC SYSTEM_CPU_INTERNAL_OPERATION_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
 ,
 IRTAC SYSTEM_CPU_MEMORY_READ_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
 ,
 IRTAC SYSTEM_CPU_MEMORY_WRITE_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
 ,
 IRTAC SYSTEM_CPU_REGISTER_READ_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
 ,
 IRTAC SYSTEM_CPU_REGISTER_WRITE_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
 ,
 IRTAC SYSTEM_DEVICE_INTERNAL_OPERATION_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
 ,
 IRTAC SYSTEM_DEVICE_MEMORY_READ_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
 ,
 IRTAC SYSTEM_DEVICE_MEMORY_WRITE_PRESERVES_INVARIANT_L2_LEMMA THEN STAC
]
QED

Definition PROOF_OBLIGATIONS_L2:
PROOF_OBLIGATIONS_L2 INVARIANT_CPU cpu_transition device_characteristics = (
  PROOF_OBLIGATION_CPU_PRESERVES_R_W_WHEN_DMAC_ACCESSES_MEMORY INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_CPU_PRESERVES_R_W_OR_SUPERSET_OF_SCRATCHPAD INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_CPU_MEMORY_WRITE_APPENDS_EXTERNAL_CONCRETE_BDS_R_W INVARIANT_CPU cpu_transition device_characteristics /\

  PROOF_OBLIGATION_READABLE_WRITABLE device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_PRESERVES_BDS_TO_FETCH device_characteristics /\
  PROOF_OBLIGATION_CPU_REGISTER_READ_MEMORY_WRITE_APPENDS_CONCRETE_BDS_R_W INVARIANT_CPU cpu_transition device_characteristics /\

  PROOF_OBLIGATION_INTERNAL_BDS_INDEPENDENT_OF_MEMORY device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_CPU_REGISTER_WRITE_SCRATCHPAD_R_W INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_CPU_REGISTER_WRITE_MEMORY_WRITE_APPENDS_CONCRETE_BDS_R_W INVARIANT_CPU cpu_transition device_characteristics /\

  PROOF_OBLIGATION_SCHEDULER device_characteristics /\
  PROOF_OBLIGATION_SCHEDULER_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_SCHEDULER_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_PROCESS_REGISTER_RELATED_MEMORY_REPLIES_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_PROCESS_REGISTER_RELATED_MEMORY_REPLIES_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_FETCH_BD_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_UPDATE_BD_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_PROCESS_REPLIES_GENERATE_REQUESTS_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_WRITE_BACK_BD_PRESERVES_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_FETCHING_BD_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_UPDATING_BD_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_PROCESS_REPLIES_GENERATE_REQUESTS_PRESERVES_BD_INTERPRETATION device_characteristics /\
  PROOF_OBLIGATION_WRITE_BACK_PRESERVES_BD_INTERPRETATION device_characteristics /\

  PROOF_OBLIGATION_REGISTER_READ_MEMORY_READ_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_MEMORY_READ_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_DECLARED_WRITE_BACK_WRITES_BD_WAS device_characteristics /\
  PROOF_OBLIGATION_WRITE_REQUESTS_CONSISTENT_WITH_BD device_characteristics /\
  PROOF_OBLIGATION_READ_REQUESTS_CONSISTENT_WITH_BD device_characteristics /\
  PROOF_OBLIGATION_DECLARED_UPDATE_WRITES_BD_WAS device_characteristics /\
  PROOF_OBLIGATION_FETCH_BD_ADDRESSES_IN_FIRST_BD_RAS device_characteristics /\
  PROOF_OBLIGATION_NO_BD_ADDRESSES_TO_READ device_characteristics /\
  PROOF_OBLIGATION_UPDATE_WRITES_DECLARED device_characteristics /\
  PROOF_OBLIGATION_WRITE_BACK_WRITES_DECLARED device_characteristics
)
End

Theorem PROOF_OBLIGATIONS_L2_LEMMA:
!INVARIANT_CPU cpu_transition device_characteristics.
  PROOF_OBLIGATIONS_L2 INVARIANT_CPU cpu_transition device_characteristics
  ==>
  PROOF_OBLIGATIONS_DMA_L1 device_characteristics /\
  PROOF_OBLIGATIONS_DMA_L2 device_characteristics /\
  PROOF_OBLIGATIONS_CPU_L2 INVARIANT_CPU cpu_transition device_characteristics /\
  PROOF_OBLIGATION_INTERNAL_DEVICE_OPERATION_L2 device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_MEMORY_READ_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_READ_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_MEMORY_READ_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_REGISTER_WRITE_MEMORY_WRITE_REQUESTS_ADDRESS_SCRATCHPAD device_characteristics /\
  PROOF_OBLIGATION_CPU_PRESERVES_R_W_WHEN_DMAC_ACCESSES_MEMORY INVARIANT_CPU cpu_transition device_characteristics
Proof
INTRO_TAC THEN
ETAC PROOF_OBLIGATIONS_L2 THEN
ETAC proof_obligations_dma_l1Theory.PROOF_OBLIGATIONS_DMA_L1 THEN
ETAC proof_obligations_dma_l2Theory.PROOF_OBLIGATIONS_DMA_L2 THEN
ETAC proof_obligations_dma_l2Theory.PROOF_OBLIGATION_INTERNAL_DEVICE_OPERATION_L2 THEN
ETAC proof_obligations_cpu_l2Theory.PROOF_OBLIGATIONS_CPU_L2 THEN
STAC
QED

Theorem SYSTEM_PRESERVES_INVARIANT_L1_L2_LEMMA:
!cpu_transition INVARIANT_CPU device_characteristics cpu1 cpu2 memory1 memory2 device1 device2 operation.
  PROOF_OBLIGATIONS_L2 INVARIANT_CPU cpu_transition device_characteristics /\
  INVARIANT_CPU memory1 cpu1 /\
  ABSTRACT_CONCRETE_BDS_TO_FETCH_EQ device_characteristics memory1 device1 device1 /\
  system_transition cpu_transition (device_transition_l2 device_characteristics) (cpu1, memory1, device1) operation (cpu2, memory2, device2) /\
  INVARIANT_L1 device_characteristics memory1 device1 /\
  INVARIANT_L2 device_characteristics memory1 device1
  ==>
  INVARIANT_L1 device_characteristics memory2 device2 /\
  INVARIANT_L2 device_characteristics memory2 device2
Proof
INTRO_TAC THEN
IRTAC PROOF_OBLIGATIONS_L2_LEMMA THEN
CONJS_TAC THENL
[
 INST_IMP_TAC l1_eq_l2Theory.SYSTEM_BSIM_L1_L2_LEMMA THEN
 QLRTAC THEN
 IRTAC l1_systemTheory.SYSTEM_PRESERVES_INVARIANT_L1_LEMMA THEN
 STAC
 ,
 IRTAC SYSTEM_PRESERVES_INVARIANT_L2_LEMMA THEN
 STAC
]
QED

val _ = export_theory();

